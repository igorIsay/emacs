* Package Management
#+begin_src emacs-lisp
  (require 'package)

  (setq package-archives '(("melpa" . "https://melpa.org/packages/")
			   ("org" . "https://orgmode.org/elpa/")
			   ("elpa" . "https://elpa.gnu.org/packages/")))

  (package-initialize)

  (unless package-archive-contents
  (package-refresh-contents))

  (unless (package-installed-p 'use-package)
  (package-install 'use-package))

  (require 'use-package)
  (setq use-package-always-ensure t)

  (unless (package-installed-p 'try)
  (package-install 'try))

#+end_src
* Enviroment Variables
#+begin_src emacs-lisp
(setenv "PATH" (concat (getenv "PATH") ":/home/igor/.local/bin"))
#+end_src
* Desktop Enviroment
#+begin_src conf-unix :tangle ~/.emacs.d/exwm/Xmodmap

  remove Lock = Caps_Lock
  remove Control = Control_L
  remove Lock = Control_L
  remove Control = Caps_Lock
  keysym Control_L = Caps_Lock
  keysym Caps_Lock = Control_L
  add Lock = Caps_Lock
  add Control = Control_L

#+end_src


#+begin_src emacs-lisp
  (defun efs/exwm-update-class ()
    (exwm-workspace-rename-buffer exwm-class-name))

  (defun exwm/run-in-background (command)
  (let ((command-parts (split-string command "[ ]+")))
    (apply #'call-process `(,(car command-parts) nil 0 nil ,@(cdr command-parts)))))

  (use-package exwm
    :config
    ;; Set the default number of workspaces
    (setq exwm-workspace-number 5)

    ;; When window "class" updates, use it to set the buffer name
    (add-hook 'exwm-update-class-hook #'efs/exwm-update-class)

    ;; Rebind CapsLock to Ctrl
    (start-process-shell-command "xmodmap" nil "xmodmap ~/.emacs.d/exwm/Xmodmap")

    ;; Set the screen resolution (update this to be the correct resolution for your screen!)
    (require 'exwm-randr)
    (exwm-randr-enable)
    (start-process-shell-command "xrandr" nil "xrandr --output eDP-1 --primary --mode 1920x1080 --pos 0x0 --rotate normal --output DP-1 --off --output HDMI-1 --mode 3840x2160 --pos 1920x0 --rotate normal")
    (setq exwm-randr-workspace-monitor-plist '(1 "eDP-1" 0 "HDMI-1"))

    ;; Load the system tray before exwm-init
    (require 'exwm-systemtray)
    (exwm-systemtray-enable)

    ;; using xim input
    (require 'exwm-xim)
    (exwm-xim-enable)
    (push ?\C-\\ exwm-input-prefix-keys)   ;; use Ctrl + \ to switch input method

    ;; These keys should always pass through to Emacs
    (setq exwm-input-prefix-keys
      '(?\C-x
        ?\C-u
        ?\C-h
        ?\M-x
        ?\M-`
        ?\M-&
        ?\M-:
        ?\C-\\    ;; Change input method
        ?\C-\M-j  ;; Buffer list
        ?\C-\ ))  ;; Ctrl+Space

    ;; Ctrl+Q will enable the next key to be sent directly
    (define-key exwm-mode-map [?\C-q] 'exwm-input-send-next-key)

    ;; Set up global key bindings.  These always work, no matter the input state!
    ;; Keep in mind that changing this list after EXWM initializes has no effect.
    (setq exwm-input-global-keys
          `(
            ;; Reset to line-mode (C-c C-k switches to char-mode via exwm-input-release-keyboard)
            ([?\s-r] . exwm-reset)

            ;; Move between windows
            ([s-left] . windmove-left)
            ([s-right] . windmove-right)
            ([s-up] . windmove-up)
            ([s-down] . windmove-down)

            ;; Launch applications via shell command
            ([?\s-&] . (lambda (command)
                         (interactive (list (read-shell-command "$ ")))
                         (start-process-shell-command command nil command)))

            ;; Switch workspace
            ([?\s-w] . exwm-workspace-switch)
            ([?\s-`] . (lambda () (interactive) (exwm-workspace-switch-create 0)))

            ;; 's-N': Switch to certain workspace with Super (Win) plus a number key (0 - 9)
            ,@(mapcar (lambda (i)
                        `(,(kbd (format "s-%d" i)) .
                          (lambda ()
                            (interactive)
                            (exwm-workspace-switch-create ,i))))
                      (number-sequence 0 9))))

    (exwm-enable))

  ;; Launch apps that will run in the background
  (exwm/run-in-background "nm-applet")
  (exwm/run-in-background "udiskie -t")

  (use-package volume
    :config
    (setq volume-amixer-card 1))
#+end_src
* User Interface

#+begin_src emacs-lisp

  (load-theme 'tango-dark)

  (global-display-line-numbers-mode)
  (setq inhibit-startup-message t)
  (scroll-bar-mode -1)
  (tool-bar-mode -1)
  (tooltip-mode -1)
  (set-fringe-mode 10)
  (menu-bar-mode -1)
  (display-battery-mode 1)
  (display-time-mode 1)
  (setq visible-bell t)

  (use-package windresize)

  (use-package ace-window
  :init
  (progn
    (global-set-key [remap other-window] 'ace-window)
    (custom-set-faces
     '(aw-leading-char-face
       ((t (:inherit ace-jump-face-foreground :height 3.0)))))
    ))

#+end_src

* Completion System

#+begin_src emacs-lisp
  (use-package counsel)

  (use-package ivy
    :bind (("C-s" . swiper-isearch)
	   ("C-r" . swiper-isearch-backward))
    :config
    (ivy-mode 1))

  (use-package company
      :ensure t
      :config
      (setq company-idle-delay 0.3)
      (global-company-mode 1)
      (global-set-key (kbd "C-<tab>") 'company-complete)
      (setq lsp-completion-provider :capf))

#+end_src

* Development
** Projectile
#+begin_src emacs-lisp
  (use-package projectile
    :init (projectile-mode +1)
    :custom ((projectile-completion-system 'ivy))
    :bind (:map projectile-mode-map
                ("C-c p" . projectile-command-map)))

  (use-package counsel-projectile
    :config (counsel-projectile-mode))

  (use-package projectile-ripgrep)

  (setq projectile-project-search-path '(projects-path))
  (add-to-list 'projectile-globally-ignored-directories "./storages")
#+end_src
** Magit, Flycheck, Restclient
#+begin_src emacs-lisp

  (use-package magit)

  (use-package flycheck)
  (global-flycheck-mode t)

  (use-package restclient)
  (add-to-list 'auto-mode-alist '("\\.api$" . restclient-mode))

#+end_src
** Whitespace, Intend
#+begin_src emacs-lisp
  (setq whitespace-style
      (quote (face
              trailing
              tabs
              empty
              indention
              spaces
              space-mark
              space-after-tab
              space-before-tab
              tab-mark)))

  (setq whitespace-space-regexp "\\(^ +\\)")
  (setq whitespace-hspace-regexp "\\(^\xA0+\\)")

  (setq whitespace-display-mappings
      '((face)
          (space-mark 32 [32] [46])
          (tab-mark 9 [8594 9] [92 9])))

#+end_src

#+RESULTS:

** Modes
#+begin_src emacs-lisp
    (use-package php-mode
      :mode "\\.php$")

    (use-package rjsx-mode
      :mode ("\\.js$" . rjsx-mode)
      :config (setq sgml-basic-offset 4))

    (use-package log4j-mode
      :init
      (add-hook #'log4j-mode-hook #'read-only-mode)
      (add-hook #'log4j-mode-hook #'end-of-buffer)
      (add-hook #'log4j-mode-hook #'auto-revert-tail-mode))

    (add-hook 'prog-mode-hook '(lambda ()
                                 (whitespace-mode 1)))

    (use-package go-mode
      :mode "\\.go$")

    (use-package csv-mode
      :mode "\\.csv$"
      :config (setq csv-separators '(";" "," "\t")))
#+end_src
** LSP

#+begin_src emacs-lisp
  (use-package lsp-mode
    :hook
    (php-mode . lsp)
    (python-mode . lsp)
    ((js2-mode rjsx-mode) . lsp)
    :config
      (custom-set-variables
         '(lsp-clients-php-server-command
           '("php7.0" "/home/igor/.composer/vendor/felixfbecker/php-language-server.php"))
         '(lsp-php-show-file-parse-notifications nil)
         '(lsp-php-workspace-root-detectors
           '(lsp-php-root-projectile lsp-php-root-composer-json "index.php" "robots.txt"))
         '(lsp-phpactor-path "/home/igor/.composer/vendor/phpactor/phpactor/bin"))
    :commands lsp)

  (use-package lsp-ui
    :requires lsp-mode flycheck)


#+end_src

#+RESULTS:

** DB connections
#+begin_src emacs-lisp
  (setq sql-connection-alist
      '((divorcer-local (sql-product 'postgres)
                    (sql-port divorcer-local-db-port)
                    (sql-server divorcer-local-db-server)
                    (sql-user divorcer-local-db-user)
                    (sql-password divorcer-local-db-password)
                    (sql-database divorcer-local-db-database))
        (pp-local (sql-product 'postgres)
                    (sql-port pp-local-db-port)
                    (sql-server pp-local-db-server)
                    (sql-user pp-local-db-user)
                    (sql-password pp-local-db-password)
                    (sql-database pp-local-db-database))
       (divorcer-stage (sql-product 'postgres)
                      (sql-port divorcer-stage-db-password)
                      (sql-server divorcer-stage-db-server)
                      (sql-user divorcer-stage-db-user)
                      (sql-password divorcer-stage-db-password)
                      (sql-database divorcer-stage-db-database))
        (divorcer-prod (sql-product 'postgres)
                      (sql-port divorcer-prod-db-port)
                      (sql-server divorcer-prod-db-server)
                      (sql-user divorcer-prod-db-user)
                      (sql-password divorcer-prod-db-password)
                      (sql-database divorcer-prod-db-database))
        (pp-prod (sql-product 'postgres)
                 (sql-port pp-prod-db-port)
                 (sql-server pp-prod-db-server)
                 (sql-user pp-prod-db-user)
                 (sql-password pp-prod-db-port)
                 (sql-database pp-prod-db-database))))
#+end_src
** Stack Exchange
#+begin_src emacs-lisp
  (use-package sx)
#+end_src

** Typescript
#+begin_src emacs-lisp
    (use-package tide
      :ensure t
      :config
      (setq tide-format-options '(:indentSize 4 :tabSize 4 :convertTabsToSpaces t :baseIndentSize 0))
      :after (typescript-mode company flycheck)
      :hook ((typescript-mode . tide-setup)
             (typescript-mode . tide-hl-identifier-mode)
             (before-save . tide-format-before-save)))
#+end_src
** Commands
*** Docker
#+begin_src emacs-lisp
  (defun run-shell-with-command (buff-name command)
    (shell buff-name)
    (let ((proc (get-buffer-process buff-name))
          (buff (get-buffer buff-name))
          (command-and-go (concat command "\n")))
      (with-current-buffer buff
        (goto-char (process-mark proc))
        (insert command-and-go)
        (move-marker (process-mark proc) (point))
        )
      (process-send-string proc command-and-go)))

  (defun docker-pp ()
    (interactive)
    (run-shell-with-command "docker-pp-admin-api" (format "docker-compose -f %s up" pp-admin-api-docker-compose-file))
    (run-shell-with-command "docker-pp-admin-ui" (format "docker-compose -f %s up" pp-admin-ui-docker-compose-file))
    (run-shell-with-command "npm-pp-admin" (format "cd %s; npm run dev-start" pp-admin-ui-path))
    (run-shell-with-command "docker-pp-client-api" (format "docker-compose -f %s up" pp-client-api-docker-compose-file))
    (run-shell-with-command "docker-pp-client-ui" (format "docker-compose -f %s up" pp-client-ui-docker-compose-file))
    (run-shell-with-command "npm-pp-client" (format "cd %s; npm run dev-start" pp-client-ui-path))
    (run-shell-with-command "docker-pp-writer-api" (format "docker-compose -f %s up" pp-writer-api-docker-compose-file))
    (run-shell-with-command "docker-pp-writer-ui" (format "docker-compose -f %s up" pp-writer-ui-docker-compose-file))
    (run-shell-with-command "npm-pp-writer" (format "cd %s; npm run dev-start" pp-writer-ui-path)))
#+end_src
*** Laravel
#+begin_src emacs-lisp
  (defun laravel-log (method string)
    (concat "\\Illuminate\\Support\\Facades\\Log::" method "(" string ");")
    )

  (defun laravel-query-log ()
  (interactive)
  (let (pos1 pos2 bds)
    (let ((text-begin "\\Illuminate\\Support\\Facades\\DB::connection()->enableQueryLog();\n")
         (text-end (concat (laravel-log "info" "\\Illuminate\\Support\\Facades\\DB::getQueryLog()") "\n"))
    (if (and transient-mark-mode mark-active)
        (progn
          (goto-char (region-end))
          (insert text-end)
          (goto-char (region-beginning))
          (insert text-begin))
      (progn
        (setq bds (bounds-of-thing-at-point 'symbol))
        (goto-char (cdr bds))
        (insert text-end)
        (goto-char (car bds))
        (insert text-begin)))))))

  (defun laravel-log-info (string)
    (interactive "M")
    (insert (laravel-log "info" string))
  )
#+end_src
* Org Mode
** Org Configuration
#+begin_src emacs-lisp
  (setq-default fill-column 80)

  ;; Turn on indentation and auto-fill mode for Org files
  (defun dw/org-mode-setup ()
    (org-indent-mode)
    (variable-pitch-mode 1)
    (auto-fill-mode 0)
    (visual-line-mode 1)
    (diminish org-indent-mode))

  (use-package org
    :defer t
    :hook (org-mode . dw/org-mode-setup)
    :config
    (setq org-ellipsis " ▾"
	  org-hide-emphasis-markers t
	  org-src-fontify-natively t
	  org-fontify-quote-and-verse-blocks t
	  org-src-tab-acts-natively t
	  org-edit-src-content-indentation 2
	  org-hide-block-startup nil
	  org-src-preserve-indentation nil
	  org-startup-folded 'content
	  org-cycle-separator-lines 2
	  org-capture-bookmark nil)

    (setq org-modules
      '(org-crypt
	  org-habit
	  org-bookmark
	  org-eshell
	  org-irc))

    (setq org-refile-targets '((nil :maxlevel . 1)
			       (org-agenda-files :maxlevel . 1)))

    (setq org-outline-path-complete-in-steps nil)
    (setq org-refile-use-outline-path t)

    (org-babel-do-load-languages
      'org-babel-load-languages
      '((emacs-lisp . t)
	(ledger . t)))

    (push '("conf-unix" . conf-unix) org-src-lang-modes))
#+end_src

** Fonts and Bullets
#+begin_src emacs-lisp
  (use-package org-superstar
    :after org
    :hook (org-mode . org-superstar-mode)
    :custom
    (org-superstar-remove-leading-stars t)
    (org-superstar-headline-bullets-list '("◉" "○" "●" "○" "●" "○" "●")))

  ;; Increase the size of various headings
  ;(set-face-attribute 'org-document-title nil :font "Iosevka Aile" :weight 'bold :height 1.3)
  (dolist (face '((org-level-1 . 1.2)
		  (org-level-2 . 1.1)
		  (org-level-3 . 1.05)
		  (org-level-4 . 1.0)
		  (org-level-5 . 1.1)
		  (org-level-6 . 1.1)
		  (org-level-7 . 1.1)
		  (org-level-8 . 1.1))))
   ; (set-face-attribute (car face) nil :font "Iosevka Aile" :weight 'medium :height (cdr face)))

  ;; Make sure org-indent face is available
  (require 'org-indent)

  ;; Ensure that anything that should be fixed-pitch in Org files appears that way
  (set-face-attribute 'org-block nil :foreground nil :inherit 'fixed-pitch)
  (set-face-attribute 'org-table nil  :inherit 'fixed-pitch)
  (set-face-attribute 'org-formula nil  :inherit 'fixed-pitch)
  (set-face-attribute 'org-code nil   :inherit '(shadow fixed-pitch))
  (set-face-attribute 'org-indent nil :inherit '(org-hide fixed-pitch))
  (set-face-attribute 'org-verbatim nil :inherit '(shadow fixed-pitch))
  (set-face-attribute 'org-special-keyword nil :inherit '(font-lock-comment-face fixed-pitch))
  (set-face-attribute 'org-meta-line nil :inherit '(font-lock-comment-face fixed-pitch))
  (set-face-attribute 'org-checkbox nil :inherit 'fixed-pitch)

  ;; Get rid of the background on column views
  (set-face-attribute 'org-column nil :background nil)
  (set-face-attribute 'org-column-title nil :background nil)
#+end_src
** Block Templates
#+begin_src emacs-lisp
  (require 'org-tempo)

  (add-to-list 'org-structure-template-alist '("sh" . "src sh"))
  (add-to-list 'org-structure-template-alist '("el" . "src emacs-lisp"))
  (add-to-list 'org-structure-template-alist '("li" . "src lisp"))
  (add-to-list 'org-structure-template-alist '("sc" . "src scheme"))
  (add-to-list 'org-structure-template-alist '("ts" . "src typescript"))
  (add-to-list 'org-structure-template-alist '("py" . "src python"))
  (add-to-list 'org-structure-template-alist '("go" . "src go"))
  (add-to-list 'org-structure-template-alist '("yaml" . "src yaml"))
  (add-to-list 'org-structure-template-alist '("json" . "src json"))

  (use-package ob-php
    :config
    (add-to-list 'org-structure-template-alist '("php" . "src php")))
#+end_src
** Auto-tangle Configuration Files
#+begin_src emacs-lisp
  (defun org-bable-tangle-config ()
    (let ((bundle-filename "emacs-config-bundle.org"))
      (let ((config-path "~/projects/emacs/emacs.org")
            (local-config-path "~/projects/emacs/emacs.local.org")
            (bundle-path (concat "~/projects/emacs/dist/" bundle-filename))
            (org-confirm-babel-evaluate nil))
        (when (or (string-equal (buffer-file-name) (expand-file-name config-path))
                  (string-equal (buffer-file-name) (expand-file-name local-config-path)))
          (shell-command (format "echo '#+PROPERTY: header-args:emacs-lisp :tangle ~/.emacs' > %s; cat %s >> %s; cat %s >> %s;" bundle-path local-config-path bundle-path config-path bundle-path))
          (find-file (expand-file-name bundle-path))
          (org-babel-tangle)
          (kill-buffer bundle-filename)))))

  (add-hook 'org-mode-hook (lambda () (add-hook 'after-save-hook #'org-bable-tangle-config)))
#+end_src
* Jumping with Avy
#+begin_src emacs-lisp

  (use-package avy
    :config
      (bind-key* "C-'" 'avy-goto-char-2))

#+end_src
